---
title: "R Notebook"
output: html_notebook
---

```{r}
library(tidyverse)
library(rstan)
library(tidybayes)

d = read_csv('~/Documents/PhD Code/PKBayes/data/apixiban_regression_data.csv')


XCl = d %>% 
  distinct(Subject, .keep_all = T) %>% 
  model.matrix(~Sex + Weight, data = .)

Xage = d %>% 
  distinct(Subject, .keep_all = T) %>% 
  model.matrix(~Age-1, data = .)


XCl[,3] = (XCl[,3] - mean(XCl[,3]))/sd(XCl[,3])

Xage[,1] = (Xage - mean(Xage))/sd(Xage)


model_data = list(
  yobs = d$Concentration_scaled,
  subjectids = as.integer(factor(d$Subject)),
  n_subjects = 36,
  times = d$Time,
  N = nrow(d),
  XCl = XCl[, 2:3],
  Xage = Xage
  
)



```


```{stan, output.var='model'}
data{
  int N; //Total number of observations
  int subjectids[N]; 
  int n_subjects;
  vector[N] times;
  real yobs[N];
  matrix[36, 2] XCl;
  
}
parameters{
  real<lower=0>  mu_CL;
  vector[2] beta_CL;
  real<lower=0> s_CL;
  vector[n_subjects] z_CL;
  
  real<lower=0> mu_t;
  real<lower=0> s_t;
  vector[n_subjects] z_t;
  
  vector<lower=0, upper=1>[n_subjects] alpha ;
  
  real<lower=0, upper=1> phi;
  real<lower=0, upper=1> kappa;
  vector<lower=0, upper=1>[n_subjects] delays;
  
  real<lower=0> disp;
}
transformed parameters{
  vector[n_subjects] Cl = exp(mu_CL + XCl*beta_CL + z_CL*s_CL);
  vector[n_subjects] t = exp(mu_t + z_t*s_t);
  vector[n_subjects] ka = log(alpha)./(t .* (alpha-1));
  vector[n_subjects] ke = alpha .* ka;
  vector[N] delayed_times = times - 0.5*delays[subjectids];
  
  vector[N] C = (2.5 ./ Cl[subjectids]) .* (ke[subjectids] .* ka[subjectids]) ./ (ke[subjectids] - ka[subjectids]) .* (exp(-ka[subjectids] .* delayed_times) -exp(-ke[subjectids] .* delayed_times));
}
model{
  mu_CL ~ lognormal(log(log(3.3)),0.15);
  beta_CL ~ normal(0, 0.5);
  s_CL ~ gamma(15,100);
  z_CL ~ normal(0,1);
  
  mu_t ~ normal(log(3.3), 0.25);
  s_t ~ gamma(10, 100);
  z_t ~ normal(0,1);
  
  alpha ~ beta(2,2);
  phi ~ beta(20,20);
  kappa ~ beta(20,20);
  delays ~ beta(phi/kappa, (1-phi)/kappa);
  disp ~ cauchy(0,1);
  yobs ~ gamma(C .* C ./ disp, C ./ disp);
}
generated quantities{
  vector[N] yppc;
  
  for (i in 1:N){
    yppc[i] = gamma_rng(C[i] * C[i] / disp, C[i] / disp);
  }
}

```


```{r}
fitcov = sampling(model,
               model_data, 
               chains = 4, 
               warmup = 2000, 
               iter = 4000)
```

```{r}
fitcov %>% 
  spread_draws(C[i], yppc[i], n = 2000) %>% 
  mean_qi() %>% 
  bind_cols(d) %>% 
  ggplot()+
  geom_line(aes(Time, C))+
  geom_ribbon(aes(Time, ymin = C.lower, ymax= C.upper), alpha = 0.5)+
  geom_ribbon(aes(Time, ymin = yppc.lower, ymax= yppc.upper), alpha = 0.25)+
  geom_point(aes(Time, Concentration_scaled ), size = 1)+
  facet_wrap(~Subject)+
  theme(aspect.ratio = 1/1.61)
```


```{r}
p = rstan::extract(fitcov)

fitdistrplus::fitdist(as.numeric(p$disp), 'lnorm')
```



```{stan, output.var = 'gen_data'}

data{
  int n_subjects;
  int N;
  int subjectids[N];
  vector[N] times;
  matrix[n_subjects, 2] X;
}
generated quantities{
  real mu_CL = lognormal_rng(0.13,0.04);
  real beta_1_cl = normal_rng(0.47, 0.07);
  real beta_2_cl = normal_rng(0.15, 0.04);
  vector[2] beta_CL = [beta_1_cl, beta_2_cl]';
  real s_CL = lognormal_rng(-1.75,0.14);
  // real phi = beta_rng(43.98, 39.47);
  // real kappa = beta_rng(10.41, 9.5);
  real disp = lognormal_rng(-9, 0.14);
  real mu_t = normal_rng(0, 0.05);
  real s_t = lognormal_rng(-1.5, 0.14);
  
  
  vector[n_subjects] z_CL;
  vector[n_subjects] z_t;
  vector[n_subjects] alpha;
  // vector[n_subjects] delays;
  vector[n_subjects] ke;
  vector[n_subjects] ka;
  vector[n_subjects] Cl;
  vector[n_subjects] tmax;
  vector[N] delayed_times = times;
  vector[N] C;
  vector[N] Cobs;
  
  for (i in 1:n_subjects){
    z_CL[i] = normal_rng(0,1);
    z_t[i] = normal_rng(0,1);
    alpha[i] = beta_rng(2,2);
    // delays[i] = beta_rng(phi/kappa, (1-phi)/kappa);
    
  }
 
   tmax = exp(mu_t + z_t*s_t);
   Cl = exp(mu_CL + X*beta_CL + z_CL * s_CL);
   ka = log(alpha) ./ (tmax .* (alpha-1));
   ke = alpha .* ka;
   
   // delayed_times = times - 0.5*delays[subjectids];
   
   C = (2.5 ./ Cl[subjectids]) .* 
   (ke[subjectids] .* ka[subjectids]) ./ (ke[subjectids] - ka[subjectids]) .* 
   (exp(-ka[subjectids] .* delayed_times) -exp(-ke[subjectids] .* delayed_times)); 
   
   
   for (i in 1:N){
     Cobs[i] = gamma_rng(C[i] * C[i] / disp, C[i] / disp);
   }
}


```


```{stan, output.var='strong_model'}
data{
  int N; //Total number of observations
  int subjectids[N]; 
  int n_subjects;
  vector[N] times;
  real yobs[N];
  matrix[n_subjects, 2] X;
  
}
parameters{
  real<lower=0>  mu_CL;
  vector[2] beta_CL;
  real<lower=0> s_CL;
  vector[n_subjects] z_CL;
  
  real<lower=0> mu_t;
  real<lower=0> s_t;
  vector[n_subjects] z_t;
  
  vector<lower=0, upper=1>[n_subjects] alpha ;

  
  real<lower=0> disp;
}
transformed parameters{
  vector[n_subjects] Cl = exp(mu_CL + X*beta_CL + z_CL*s_CL);
  vector[n_subjects] t = exp(mu_t + z_t*s_t);
  vector[n_subjects] ka = log(alpha)./(t .* (alpha-1));
  vector[n_subjects] ke = alpha .* ka;
  vector[N] delayed_times = times ;
  
  vector[N] Cpred = (2.5 ./ Cl[subjectids]) .* (ke[subjectids] .* ka[subjectids]) ./ (ke[subjectids] - ka[subjectids]) .* (exp(-ka[subjectids] .* delayed_times) -exp(-ke[subjectids] .* delayed_times));
}
model{
  mu_CL ~ lognormal(0.13,0.04);
  beta_CL[1] ~ normal(0.47, 0.07);
  beta_CL[2] ~ normal(0.15, 0.04);
  s_CL ~ lognormal(-1.75,0.14);
  z_CL ~ normal(0,1);
  
  mu_t ~ normal(0, 0.05);
  s_t ~ lognormal(-1.5, 0.14);
  z_t ~ normal(0,1);
  
  alpha ~ beta(2,2);
  disp ~ lognormal(-9, 0.14);
  yobs ~ gamma(Cpred .* Cpred ./ disp, Cpred ./ disp);
}

```


```{r}

set.seed(0)
N_subjects = 100
sample_times = seq(0.5, 12, 0.25)
ids = 1:N_subjects
pairs = crossing(ids, sample_times) 

subjectids = pairs$ids
times = pairs$sample_times
N = length(times)

X = cbind(rbinom(N_subjects,1,0.5), rnorm(N_subjects))

gen_data_model_data = list(N = N,
                         times = times,
                         subjectids = subjectids,
                         n_subjects = N_subjects,
                         X = X)




pseudo_fit = sampling(gen_data, 
                      data=gen_data_model_data,
                      algorithm='Fixed_param',
                      chains=1,
                      iter=1,
                      seed = 0)


pseudo = rstan::extract(pseudo_fit)
```


```{r}
sim_data = gen_data_model_data
sim_data$yobs = as.numeric(pseudo$Cobs)

maps = optimizing(
  strong_model,
  data = sim_data,
  verbose = TRUE,
  # algorithm = 'BFGS',
  as_vector = TRUE,
  hessian = TRUE,
  tol_obj=1e-4,
  iter=10000,
  draws = 10000
)

H = maps$hessian
S = MASS::ginv(-H)
dimnames(S) = dimnames(H)
theta_tilde = maps$theta_tilde

ypred_cols = theta_tilde[, grepl('Cpred', colnames(theta_tilde))]
map_pred = apply(ypred_cols, 2, function(x) mean(x, na.rm = T))
map_low = apply(ypred_cols, 2, function(x) quantile(x, 0.025, na.rm = T))
map_high = apply(ypred_cols, 2, function(x) quantile(x, 0.975, na.rm = T))


newd = tibble(subject = sim_data$subjectids, 
              times = sim_data$times, 
              yobs = as.numeric(pseudo$Cobs ))

newd$pred = map_pred
newd$low = map_low
newd$high = map_high
```


```{r}
newd %>% 
  filter(subject %in% sample(1:100, size = 10)) %>% 
  ggplot(aes(times, pred, ymin=low, ymax=high))+
  geom_line()+
  geom_ribbon(alpha = 0.5)+
  facet_wrap(~subject, scales = 'free_y')
```

```{r}
mcmc = sampling(strong_model, sim_data)
```

```{r}
f = mcmc %>% 
  spread_draws(Cpred[i])


subs = sample(1:100, size = 12)

f %>% 
  mean_qi() %>% 
  bind_cols(newd) %>% 
  filter(between(subject, 1,25)) %>% 
  ggplot()+
  geom_ribbon(aes(times, ymin = .lower, ymax = .upper, fill = 'mcmc'), alpha = 0.5)+
  geom_ribbon(aes(times, ymin = low, ymax = high, fill = 'MAP'), alpha = 0.5)+
  facet_wrap(~subject, scales = 'free_y')


  
```

